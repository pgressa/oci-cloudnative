plugins {
    id("com.github.johnrengelman.shadow") version "6.1.0"
    id("io.micronaut.application") version "1.5.0"
}

version = "1.0.0-SNAPSHOT"
group = "micronaut.mushop"

repositories {
    mavenCentral()
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("mushop.carts.*")
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.oracle.oci.sdk:oci-java-sdk-circuitbreaker:1.37.0'
        force 'com.oracle.oci.sdk:oci-java-sdk-common:1.37.0'
        force 'com.oracle.oci.sdk:oci-java-sdk-monitoring:1.37.0'
        force 'com.oracle.oci.sdk:oci-java-sdk-workrequests:1.37.0'
        force 'com.oracle.oci.sdk:oci-java-sdk-database:1.37.0'
    }
}

dependencies {

    annotationProcessor("io.micronaut.micrometer:micronaut-micrometer-annotation:3.4.0")
    annotationProcessor("io.micronaut.data:micronaut-data-processor")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")

    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-tracing")
    implementation("io.micronaut:micronaut-validation")

    // Metrics
    implementation("io.micronaut:micronaut-management")
    implementation("io.micronaut.micrometer:micronaut-micrometer-core:3.4.0")
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-prometheus")
    runtimeOnly("io.micronaut.oraclecloud:micronaut-oraclecloud-micrometer:1.3.5")

    // ADB access
    implementation platform("com.oracle.database.jdbc:ojdbc-bom:21.1.0.0")
    implementation("io.micronaut.data:micronaut-data-jdbc")
    runtimeOnly("com.oracle.database.jdbc:ojdbc8")
    implementation("com.oracle.database.soda:orajsoda:1.1.4")
    runtimeOnly("io.micronaut.flyway:micronaut-flyway")
    implementation("com.oracle.database.security:oraclepki")
    implementation("com.oracle.database.security:osdt_core")
    implementation("com.oracle.database.security:osdt_cert")
    compileOnly("io.micronaut.sql:micronaut-jdbc-ucp")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    implementation("io.micronaut.sql:micronaut-jdbc")
    implementation("io.micronaut.data:micronaut-data-tx")

    implementation("io.micronaut.oraclecloud:micronaut-oraclecloud-atp:1.3.5")
    runtimeOnly("com.oracle.oci.sdk:oci-java-sdk-monitoring")

    // Tracing
    implementation("io.opentracing.brave:brave-opentracing")
    implementation("io.swagger.core.v3:swagger-annotations")
    runtimeOnly("io.zipkin.brave:brave-instrumentation-http")
    runtimeOnly("io.zipkin.reporter2:zipkin-reporter")

    runtimeOnly("ch.qos.logback:logback-classic")

    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:oracle-xe")
    testImplementation("org.testcontainers:testcontainers")

    testImplementation("org.hamcrest:hamcrest")
}

nativeImage {
    args "-H:+AddAllCharsets"
    args "-H:IncludeResourceBundles=oracle.json.impl.binary.Messages"
    args "-H:IncludeResourceBundles=oracle.json.rdbms.Messages"
    args "-H:IncludeResourceBundles=oracle.json.parser.Messages"
    args "-H:IncludeResourceBundles=oracle.json.common.Messages"
    args "-H:IncludeResourceBundles=oracle.soda.rdbms.exceptions.Messages"
    args "--initialize-at-build-time=com.sun.jmx.remote.util.EnvHelp"
    args "--initialize-at-build-time=jdk.management.jfr.SettingDescriptorInfo"
}

application {
    mainClass.set("mushop.carts.Application")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

if (javaBaseImage == "graalvm") {
    dockerfile {
        baseImage = "ghcr.io/graalvm/graalvm-ce:ol8-java11-21.1.0"
    }
}

dockerBuild {
    images = ["iad.ocir.io/cloudnative-devrel/micronaut-showcase/mushop/$project.name-${javaBaseImage}:$project.version"]
}

dockerBuildNative {
  images = ["iad.ocir.io/cloudnative-devrel/micronaut-showcase/mushop/${project.name}-native:$project.version"]
}
